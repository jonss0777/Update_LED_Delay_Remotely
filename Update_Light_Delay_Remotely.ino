/*
  Program updates light delay period remotetly via AppScript + Google sheet.

  Possible extensions: Create a parser to handle more commands.
  Possible performance tests: Meassure latency and battery consumption.

  By Jonathan Suconota at ASRC

*/


#include <WiFi.h>
#include <HTTPClient.h>
#include <WiFiClientSecure.h>
#include <secrets.h>

// WiFi Credentials
// const char* ssid = "";
// const char* password = "";

// // AppScript Endpoint
// const char* endpoint = "";

WiFiClientSecure secureClient;
HTTPClient http;
int currPeriod = 2000;  // 2 seconds

// Google Certificate
const char* google_root_ca = R"string_literal(
-----BEGIN CERTIFICATE-----
MIIFVzCCAz+gAwIBAgINAgPlk28xsBNJiGuiFzANBgkqhkiG9w0BAQwFADBHMQsw
CQYDVQQGEwJVUzEiMCAGA1UEChMZR29vZ2xlIFRydXN0IFNlcnZpY2VzIExMQzEU
MBIGA1UEAxMLR1RTIFJvb3QgUjEwHhcNMTYwNjIyMDAwMDAwWhcNMzYwNjIyMDAw
MDAwWjBHMQswCQYDVQQGEwJVUzEiMCAGA1UEChMZR29vZ2xlIFRydXN0IFNlcnZp
Y2VzIExMQzEUMBIGA1UEAxMLR1RTIFJvb3QgUjEwggIiMA0GCSqGSIb3DQEBAQUA
A4ICDwAwggIKAoICAQC2EQKLHuOhd5s73L+UPreVp0A8of2C+X0yBoJx9vaMf/vo
27xqLpeXo4xL+Sv2sfnOhB2x+cWX3u+58qPpvBKJXqeqUqv4IyfLpLGcY9vXmX7w
Cl7raKb0xlpHDU0QM+NOsROjyBhsS+z8CZDfnWQpJSMHobTSPS5g4M/SCYe7zUjw
TcLCeoiKu7rPWRnWr4+wB7CeMfGCwcDfLqZtbBkOtdh+JhpFAz2weaSUKK0Pfybl
qAj+lug8aJRT7oM6iCsVlgmy4HqMLnXWnOunVmSPlk9orj2XwoSPwLxAwAtcvfaH
szVsrBhQf4TgTM2S0yDpM7xSma8ytSmzJSq0SPly4cpk9+aCEI3oncKKiPo4Zor8
Y/kB+Xj9e1x3+naH+uzfsQ55lVe0vSbv1gHR6xYKu44LtcXFilWr06zqkUspzBmk
MiVOKvFlRNACzqrOSbTqn3yDsEB750Orp2yjj32JgfpMpf/VjsPOS+C12LOORc92
wO1AK/1TD7Cn1TsNsYqiA94xrcx36m97PtbfkSIS5r762DL8EGMUUXLeXdYWk70p
aDPvOmbsB4om3xPXV2V4J95eSRQAogB/mqghtqmxlbCluQ0WEdrHbEg8QOB+DVrN
VjzRlwW5y0vtOUucxD/SVRNuJLDWcfr0wbrM7Rv1/oFB2ACYPTrIrnqYNxgFlQID
AQABo0IwQDAOBgNVHQ8BAf8EBAMCAYYwDwYDVR0TAQH/BAUwAwEB/zAdBgNVHQ4E
FgQU5K8rJnEaK0gnhS9SZizv8IkTcT4wDQYJKoZIhvcNAQEMBQADggIBAJ+qQibb
C5u+/x6Wki4+omVKapi6Ist9wTrYggoGxval3sBOh2Z5ofmmWJyq+bXmYOfg6LEe
QkEzCzc9zolwFcq1JKjPa7XSQCGYzyI0zzvFIoTgxQ6KfF2I5DUkzps+GlQebtuy
h6f88/qBVRRiClmpIgUxPoLW7ttXNLwzldMXG+gnoot7TiYaelpkttGsN/H9oPM4
7HLwEXWdyzRSjeZ2axfG34arJ45JK3VmgRAhpuo+9K4l/3wV3s6MJT/KYnAK9y8J
ZgfIPxz88NtFMN9iiMG1D53Dn0reWVlHxYciNuaCp+0KueIHoI17eko8cdLiA6Ef
MgfdG+RCzgwARWGAtQsgWSl4vflVy2PFPEz0tv/bal8xa5meLMFrUKTX5hgUvYU/
Z6tGn6D/Qqc6f1zLXbBwHSs09dR2CQzreExZBfMzQsNhFRAbd03OIozUhfJFfbdT
6u9AWpQKXCBfTkBdYiJ23//OYb2MI3jSNwLgjt7RETeJ9r/tSQdirpLsQBqvFAnZ
0E6yove+7u7Y/9waLd64NnHi/Hm3lCXRSHNboTXns5lndcEZOitHTtNCjv0xyBZm
2tIMPNuzjsmhDYAPexZ3FL//2wmUspO8IFgV6dtxQ/PeEMMA3KgqlbbC1j+Qa3bb
bP6MvPJwNQzcmRk13NfIRmPVNnGuV/u3gm3c
-----END CERTIFICATE-----
)string_literal";


void connectToWIFI() {
  // Set WiFi to Station mode
  WiFi.mode(WIFI_STA);
  WiFi.begin(ssid, password);
  Serial.print("Connecting to WiFi ..");

  // Wait for connection
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  Serial.println("");
  Serial.println("Connected to the WiFi network");
  Serial.print("IP Address: ");
  Serial.println(WiFi.localIP());  // Print local IP
}


void doGET() {
  http.begin(endpoint);  // Specify URL with parameters
  int httpResponseCode = http.GET();                        // Send the GET request

  if (httpResponseCode > 0) {
    String payload = http.getString();  // Get the response body
    Serial.println(httpResponseCode);
    Serial.println(payload);
  } else {
    Serial.print("Error on HTTP GET request: ");
    Serial.println(httpResponseCode);
  }
  http.end();  // Free resources
}

void doPost(String payloadData) {

  // --- First POST to Apps Script ---
  http.begin(secureClient, endpoint);
  http.addHeader("Content-Type", "text/plain");

  int code = http.POST(payloadData);
  String response = http.getString();
  Serial.println("POST response code: " + String(code));
  Serial.println("POST response: " + response);

  // --- Check for redirect ---
  if (code == 302 || code == 301) {
    String redirectURL = http.getLocation();
    Serial.println("Redirecting to: " + redirectURL);

    http.end(); // close old connection

    // --- Open new connection to redirected URL ---
    http.begin(secureClient, redirectURL);
    code = http.GET();
    response = http.getString();
    Serial.println("GET response code: " + String(code));
    Serial.println("GET response: " + response);
    currPeriod = response.toInt();
  }

  http.end(); // close final connection
}

// Set up and start program.
const int LED_BUILTIN = 2;
void setup() {
  Serial.begin(115200);
  while (!Serial) {
    ;  // Wait for serial port to connect
  }
  Serial.println("Connected!");

  // initialize digital pin LED_BUILTIN as an output.
  pinMode(LED_BUILTIN, OUTPUT);
  connectToWIFI();
  secureClient.setCACert(google_root_ca);
}

// the loop function runs over and over again forever
// the off state takes a little longer because of the doPost()
void loop() {
  doPost(String(currPeriod));                         // updates currPeriod
  Serial.println(currPeriod);
  digitalWrite(LED_BUILTIN, HIGH);  // turn the LED on (HIGH is the voltage level)
  delay(currPeriod);
  digitalWrite(LED_BUILTIN, LOW);
  delay(currPeriod);
}